apply plugin: 'com.android.model.native'

def lib_distribution_root = '../distribution'
model {
    repositories {
        libs(PrebuiltLibraries) {
            OpenNI2 {
                headers.srcDir "${lib_distribution_root}/OpenNI2/include"
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("${lib_distribution_root}/OpenNI2/lib/${targetPlatform.getName()}/libOpenNI2.so")
                }
            }
            XnLib {
                headers.srcDir "${lib_distribution_root}/XnLib/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("${lib_distribution_root}/XnLib/lib/${targetPlatform.getName()}/libXnLib.a")
                }
            }
            usb {
                headers.srcDir "${lib_distribution_root}/usb/include"
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("${lib_distribution_root}/usb/lib/${targetPlatform.getName()}/libusb.so")
                }
            }
        }
    }

    android {
        compileSdkVersion project.compileSdkVersion
        buildToolsVersion project.buildToolsVersion

        defaultConfig {
            minSdkVersion.apiLevel project.minSdkVersion
            targetSdkVersion.apiLevel project.targetSdkVersion
            versionCode = 1
            versionName = '1.0'
        }

        ndk {
            moduleName = 'PSLink'
            cppFlags.addAll([
                    '-O3',
                    '-ftree-vectorize',
                    '-ffast-math',
                    '-funroll-loops',
                    '-fPIC',
                    '-fvisibility=hidden',
//                    '-DHAVE_NEON=1',
//                    '-DXN_NEON',
//                    '-flax-vector-conversions',
                    '-DANDROID=1',
                    '-D__arm__=1',
                    '-I' + file("${lib_distribution_root}/XnLib/include/"),
                    '-I' + file('../submodule/OpenNI2/Include/'),
                    '-I' + file('src/main/jni/PSLink/'),
                    '-I' + file('src/main/jni/PSLink/LinkProtoLib/'),
                    '-I' + file('src/main/jni/PSLink/Protocols/XnLinkProto/'),
            ])
            ldLibs.addAll(['android', 'log'])
        }
        sources {
            main {
                jni {
                    dependencies {
                        project ':XnLib' linkage 'static'
                        library 'OpenNI2' linkage 'shared'
                        library 'usb' linkage 'shared'
                    }
                }
            }
        }
    }
}

task(distributeLib, type: Copy) {
    dependsOn assemble
    into '../distribution/PSLink/'
    from('src/main/jni/libPSLink.h') {
        into 'include/'
    }
    from('build/outputs/native/release/lib') {
        into 'lib/'
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.contains('compile')) {
        task.dependsOn ':OpenNI2:distributeLib'
        task.dependsOn ':XnLib:distributeLib'
        task.dependsOn ':usb:distributeLib'
    }
}